#!perl

use 5.010001;
use strict;
use warnings;
use Log::ger;
use Log::ger::Screen;

# AUTHORITY
# DATE
# DIST
# VERSION

sub _strip {
    my $str = shift;
    $str =~ s/<.+?>//g;
    $str;
}

$/ = "";
my $num = 1;
while (my $para = <>) {
    unless ($para =~ /\A([0-9][^\n]+)(.+)/s) {
        log_trace "Paragraph does not begin with a digit, skipping";
        next;
    }

    my ($first_line, $subseq_lines) = ($1, $2);

    $first_line =~ s/[.]/,/g;

    print $num, "\n", _strip($first_line), _strip($subseq_lines);
    $num++;
}

# ABSTRACT: Convert .vtt to .srt
# PODNAME:

=head1 SYNOPSIS

 % vtt2srt < INPUT.vtt > OUTPUT.srt


=head1 SEE ALSO

B<youtube-dl> downloads subtitles in .vtt format by default, but can be
instructed to download and/or convert subtitles to .srt format (see options:
C<--sub-format vtt>, C<--convert-subtitles srt>).

=head1 cut
